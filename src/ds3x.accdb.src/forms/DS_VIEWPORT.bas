Version =20
VersionRequired =20
Begin Form
    PopUp = NotDefault
    RecordSelectors = NotDefault
    FastLaserPrinting = NotDefault
    ShortcutMenu = NotDefault
    AutoCenter = NotDefault
    NavigationButtons = NotDefault
    DividingLines = NotDefault
    KeyPreview = NotDefault
    DataEntry = NotDefault
    AllowDesignChanges = NotDefault
    DefaultView =0
    ScrollBars =0
    ViewsAllowed =1
    TabularCharSet =177
    TabularFamily =0
    PictureAlignment =2
    DatasheetGridlinesBehavior =3
    GridY =10
    Width =3435
    DatasheetFontHeight =11
    ItemSuffix =1568
    Left =3225
    Top =3030
    Right =21780
    Bottom =15225
    OnUnload ="[Event Procedure]"
    RecSrcDt = Begin
        0x4a0577b4d2d8e540
    End
    DatasheetFontName ="Calibri"
    OnKeyDown ="[Event Procedure]"
    OnKeyUp ="[Event Procedure]"
    AllowDatasheetView =0
    FilterOnLoad =0
    SplitFormDatasheet =1
    SplitFormDatasheet =1
    ShowPageMargins =0
    DisplayOnSharePointSite =1
    DatasheetAlternateBackColor =15921906
    DatasheetGridlinesColor12 =0
    FitToScreen =255
    DatasheetBackThemeColorIndex =1
    BorderThemeColorIndex =3
    ThemeFontIndex =1
    ForeThemeColorIndex =0
    AlternateBackThemeColorIndex =1
    AlternateBackShade =95.0
    Begin
        Begin Label
            BackStyle =0
            FontSize =11
            FontName ="Calibri"
            ThemeFontIndex =1
            BackThemeColorIndex =1
            BorderThemeColorIndex =0
            BorderTint =50.0
            ForeThemeColorIndex =0
            ForeTint =50.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin Rectangle
            SpecialEffect =3
            BackStyle =0
            BorderLineStyle =0
            Width =850
            Height =850
            BorderShade =65.0
            GridlineShade =65.0
        End
        Begin CommandButton
            Width =1701
            Height =283
            FontSize =11
            FontWeight =400
            FontName ="Calibri"
            ForeThemeColorIndex =0
            ForeTint =75.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
            UseTheme =1
            Shape =1
            Gradient =12
            BackThemeColorIndex =4
            BackTint =60.0
            BorderLineStyle =0
            BorderThemeColorIndex =4
            BorderTint =60.0
            ThemeFontIndex =1
            HoverThemeColorIndex =4
            HoverTint =40.0
            PressedThemeColorIndex =4
            PressedShade =75.0
            HoverForeThemeColorIndex =0
            HoverForeTint =75.0
            PressedForeThemeColorIndex =0
            PressedForeTint =75.0
        End
        Begin OptionButton
            BorderLineStyle =0
            LabelX =230
            LabelY =-30
            BorderThemeColorIndex =1
            BorderShade =65.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin CheckBox
            BorderLineStyle =0
            LabelX =230
            LabelY =-30
            BorderThemeColorIndex =1
            BorderShade =65.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin OptionGroup
            SpecialEffect =3
            BorderLineStyle =0
            Width =1701
            Height =1701
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin BoundObjectFrame
            AddColon = NotDefault
            SizeMode =3
            SpecialEffect =2
            BorderLineStyle =0
            Width =4536
            Height =2835
            LabelX =-1701
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin TextBox
            AddColon = NotDefault
            FELineBreak = NotDefault
            BorderLineStyle =0
            Width =1701
            LabelX =-1701
            FontSize =11
            FontName ="Calibri"
            AsianLineBreak =1
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            ThemeFontIndex =1
            ForeThemeColorIndex =0
            ForeTint =75.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin ListBox
            BorderLineStyle =0
            Width =1701
            Height =1417
            LabelX =-1701
            FontSize =11
            FontName ="Calibri"
            AllowValueListEdits =1
            InheritValueList =1
            ThemeFontIndex =1
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            ForeThemeColorIndex =0
            ForeTint =75.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin ComboBox
            AddColon = NotDefault
            BorderLineStyle =0
            Width =1701
            LabelX =-1701
            FontSize =11
            FontName ="Calibri"
            AllowValueListEdits =1
            InheritValueList =1
            ThemeFontIndex =1
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            ForeThemeColorIndex =2
            ForeShade =50.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin Subform
            BorderLineStyle =0
            Width =1701
            Height =1701
            BorderThemeColorIndex =1
            GridlineThemeColorIndex =1
            GridlineShade =65.0
            BorderShade =65.0
            ShowPageHeaderAndPageFooter =1
        End
        Begin UnboundObjectFrame
            SpecialEffect =2
            OldBorderStyle =1
            Width =4536
            Height =2835
            ThemeFontIndex =1
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            ForeThemeColorIndex =2
            ForeShade =50.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin PageBreak
            Width =283
        End
        Begin CustomControl
            OldBorderStyle =1
            Width =4536
            Height =2835
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            BorderShade =65.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin Tab
            Width =5103
            Height =3402
            FontSize =11
            FontName ="Calibri"
            ThemeFontIndex =0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
            UseTheme =1
            Shape =3
            BackThemeColorIndex =1
            BackShade =85.0
            BorderLineStyle =0
            BorderThemeColorIndex =2
            BorderTint =60.0
            HoverThemeColorIndex =1
            PressedThemeColorIndex =1
            HoverForeThemeColorIndex =0
            HoverForeTint =75.0
            PressedForeThemeColorIndex =0
            PressedForeTint =75.0
            ForeThemeColorIndex =0
            ForeTint =75.0
        End
        Begin Page
            Width =1701
            Height =1701
            BorderThemeColorIndex =1
            BorderShade =65.0
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin EmptyCell
            Height =240
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin NavigationControl
            BorderWidth =1
            BorderLineStyle =0
            BackThemeColorIndex =1
            BorderThemeColorIndex =1
            GridlineThemeColorIndex =1
            GridlineShade =65.0
        End
        Begin NavigationButton
            Width =283
            Height =283
            ForeColor =-2
            GridlineThemeColorIndex =1
            GridlineShade =65.0
            HoverColor =-2
            HoverThemeColorIndex =2
            HoverTint =20.0
            PressedColor =-2
            PressedThemeColorIndex =2
            PressedTint =60.0
            HoverForeColor =-2
            HoverForeThemeColorIndex =0
            HoverForeTint =75.0
            PressedForeColor =-2
            PressedForeThemeColorIndex =0
            PressedForeTint =75.0
            BackThemeColorIndex =1
            OldBorderStyle =0
            BorderLineStyle =0
            BorderThemeColorIndex =3
            BorderShade =90.0
            ThemeFontIndex =1
            FontName ="Calibri"
            FontWeight =400
            FontSize =11
            ForeThemeColorIndex =0
            ForeTint =75.0
        End
        Begin FormHeader
            Height =645
            Name ="FormHeaders"
            AlternateBackThemeColorIndex =1
            AlternateBackShade =95.0
            BackThemeColorIndex =3
            Begin
                Begin Subform
                    CanGrow = NotDefault
                    TabStop = NotDefault
                    OverlapFlags =85
                    OldBorderStyle =0
                    Width =3433
                    Height =645
                    BorderColor =10921638
                    Name ="DS_WORKSHEET_HEADERS"
                    SourceObject ="Form.DS_WORKSHEET_HEADERS"
                    LeftPadding =0
                    TopPadding =15
                    RightPadding =0
                    BottomPadding =0
                    GridlineColor =10921638
                    GridlineWidthLeft =0
                    GridlineWidthTop =0
                    GridlineWidthRight =0
                    GridlineWidthBottom =0

                    LayoutCachedWidth =3433
                    LayoutCachedHeight =645
                End
            End
        End
        Begin Section
            CanGrow = NotDefault
            CanShrink = NotDefault
            Height =2835
            Name ="FormDetail"
            AlternateBackThemeColorIndex =4
            AlternateBackTint =40.0
            BackThemeColorIndex =1
            BackShade =60.0
            Begin
                Begin Subform
                    Locked = NotDefault
                    CanGrow = NotDefault
                    TabStop = NotDefault
                    OverlapFlags =87
                    OldBorderStyle =0
                    Width =3435
                    Height =2835
                    TabIndex =1
                    BorderColor =10921638
                    Name ="DS_WORKSHEET"
                    SourceObject ="Form.DS_WORKSHEET"
                    GroupTable =2
                    LeftPadding =0
                    TopPadding =0
                    RightPadding =0
                    BottomPadding =0
                    GridlineColor =10921638

                    LayoutCachedWidth =3435
                    LayoutCachedHeight =2835
                    LayoutGroup =1
                    GroupTable =2
                    ShowPageHeaderAndPageFooter =0
                End
                Begin CommandButton
                    OverlapFlags =93
                    TextFontCharSet =177
                    TextFontFamily =0
                    Width =15
                    Height =15
                    FontSize =1
                    FontWeight =100
                    Name ="HiddenControl"
                    Caption ="Comando1435"
                    LeftPadding =0
                    TopPadding =0
                    RightPadding =0
                    BottomPadding =0
                    GridlineColor =10921638

                    LayoutCachedWidth =15
                    LayoutCachedHeight =15
                    Alignment =1
                    ForeThemeColorIndex =1
                    ForeTint =100.0
                    Shape =0
                    Gradient =0
                    BackThemeColorIndex =1
                    BackTint =100.0
                    OldBorderStyle =0
                    BorderThemeColorIndex =1
                    BorderTint =100.0
                    HoverColor =-2
                    HoverThemeColorIndex =-1
                    HoverTint =80.0
                    PressedColor =-2
                    PressedThemeColorIndex =-1
                    PressedShade =80.0
                    HoverForeColor =0
                    HoverForeTint =100.0
                    PressedForeColor =0
                    PressedForeTint =100.0
                    QuickStyle =4
                    QuickStyleMask =-1009
                    Overlaps =1
                End
            End
        End
        Begin FormFooter
            Visible = NotDefault
            Height =0
            Name ="FormFooter"
            AlternateBackThemeColorIndex =1
            AlternateBackShade =95.0
            BackThemeColorIndex =1
        End
    End
End
CodeBehindForm
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
'@Folder "ds3x.UI.Scrollview"
Option Compare Database
Option Explicit


Private pWorksheet As Form_DS_WORKSHEET
Private pWorksheetHeaders As Form_DS_WORKSHEET_HEADERS
Private pWorksheetNumbers As Form_DS_WORKSHEET_NUMBERS
Private pScrollview As Form_DS_SCROLLVIEW

' The current ColumnsToLargeChangeTrack value used in CachedTracks, CachedTracks resets when this value changes
Private pTrackColumnSizesInCache As Long
Const NumPagesInLargeChangeRows As Long = 5
Const PageSize As Long = 10
Const PageCount As Long = 10

Private Type TViewportState
    ScrollPosX As Double
    ScrollPosY As Double
    ' Index of current visible track in Viewport
    TrackIndex As Long
    ' Index of current visible page in Viewport
    PageIndex As Long
    ' Index of the first visible column in **Table**
    FirstVisibleColumn As Long
    ' Index of the first visible row in **Table**
    FirstVisibleRow As Long
    ' The distance between the start of the first visible column to the viewport left edge (must be less than GridCellSizeX)
    FirstVisibleColumnPositionModX As Long
    ' The distance between the start of the first visible row to the viewport top edge (must be less than GridCellSizeY)
    FirstVisibleRowPositionModY As Long
    ' Index of the first visible column relative to current visible **Track**
    FirstVisibleColumnInTrack As Long
    ' Index of the first visible row relative to current visible **Page**
    FirstVisibleRowInPage As Long
    ' Number of columns as the distance between track switching
    ColumnsToLargeChangeTrack As Long
    ' The distance from the current track left edge to the viewport left edge
    TrackPositionModX As Long
    ' The distance from the current page top edge to the viewport top edge
    PagePositionModY As Long
End Type

Private This As TViewportState

' ---

Public Property Get Worksheet() As Form_DS_WORKSHEET: Set Worksheet = pWorksheet: End Property
Public Property Set Worksheet(ByRef Value As Form_DS_WORKSHEET): Set pWorksheet = Value: End Property

Public Property Get WorksheetHeaders() As Form_DS_WORKSHEET_HEADERS: Set WorksheetHeaders = pWorksheetHeaders: End Property
Public Property Set WorksheetHeaders(ByRef Value As Form_DS_WORKSHEET_HEADERS): Set pWorksheetHeaders = Value: End Property

Public Property Get WorksheetNumbers() As Form_DS_WORKSHEET_NUMBERS: Set WorksheetNumbers = pWorksheetNumbers: End Property
Public Property Set WorksheetNumbers(ByRef Value As Form_DS_WORKSHEET_NUMBERS): Set pWorksheetNumbers = Value: End Property

Public Property Get Scrollview() As Form_DS_SCROLLVIEW: Set Scrollview = pScrollview: End Property
Public Property Set Scrollview(ByRef Value As Form_DS_SCROLLVIEW): Set pScrollview = Value: End Property

Friend Property Get FirstColumnIndex() As Long: FirstColumnIndex = This.TrackIndex * This.ColumnsToLargeChangeTrack: End Property

Property Get IsSubform() As Boolean: On Error Resume Next: IsSubform = Len(Me.Parent.Name) > 0: End Property


' --- FORM EVENTS ---

Private Sub Form_Unload(Cancel As Integer)
    On Error Resume Next
    Me.TimerInterval = 0
End Sub

Private Sub Form_KeyDown(KeyCode As Integer, Shift As Integer): pScrollview.OnKeyDownHandler KeyCode, Shift: End Sub
Private Sub Form_KeyUp(KeyCode As Integer, Shift As Integer): pScrollview.OnKeyUpHandler KeyCode, Shift: End Sub


' --- SETUP / BINDING ---

Public Sub Setup()
    Dim r As ds3xGlobals.RECT, B As ds3xGlobals.BOUNDS, t As Long, c As Long
    
    Set Worksheet = Me.DS_WORKSHEET.Form
    Set pWorksheet.Viewport = Me
    Set WorksheetHeaders = Me.DS_WORKSHEET_HEADERS.Form
    Set pWorksheetHeaders.Viewport = Me
    Set pWorksheetNumbers.Viewport = Me
    pWorksheet.Setup
    pWorksheetHeaders.Setup
    pWorksheetNumbers.Setup PageSize * PageCount
    
    r = ScreenLib.GetScreenRectOfPoint(ScreenLib.PointInRect(ScreenLib.GetWindowRect(Me), DirectionType.Center), True)
    B = ScreenLib.RectToBounds(r)
    
    t = pWorksheet.MaxContentWidthLimit
    Me.Width = t
    With Me.DS_WORKSHEET_HEADERS
        .Left = 0
        .Top = 0
        .Width = t
        Me.FormHeaders.Height = .Height
    End With
    Me.FormDetail.Height = CLng(Min(B.h * 1.95, 31500))
    With Me.DS_WORKSHEET
        .Left = 0
        .Top = 0
        .Width = t
        .Height = CLng(Min(B.h * 1.95, 31500))
    End With
End Sub


' --- SCROLLING ---

Public Function PropagateMouseWheel(ByVal Page As Boolean, ByVal Count As Long)
    On Error Resume Next
    pScrollview.PropagateMouseWheel Page, Count
End Function

Public Sub ScrollTo(ByVal X As Double, ByVal Y As Double)
    Dim sView As TViewportState
    sView = GetViewportStateAt(X, Y)
    
    If This.TrackIndex <> sView.TrackIndex Or This.PageIndex <> sView.PageIndex Or pTrackColumnSizesInCache <> sView.ColumnsToLargeChangeTrack Then
        pWorksheet.Painting = False
        pWorksheetHeaders.Painting = False
        pWorksheetNumbers.Painting = False
        ScreenLib.WindowMoveTo pWorksheet, 0 - sView.TrackPositionModX, 0 - sView.PagePositionModY
        ScreenLib.WindowMoveTo pWorksheetHeaders, 0 - sView.TrackPositionModX, 0
        ScreenLib.WindowMoveTo pWorksheetNumbers, 0, 0 - sView.PagePositionModY
        pTrackColumnSizesInCache = sView.ColumnsToLargeChangeTrack
        Set pWorksheet.Recordset = GetTrack(sView.TrackIndex, sView.PageIndex).Instance
        pWorksheet.SetupGrid sView.TrackIndex * sView.ColumnsToLargeChangeTrack, sView.PageIndex * PageSize * NumPagesInLargeChangeRows, pScrollview.Table
        pWorksheetHeaders.SetupGrid sView.TrackIndex * sView.ColumnsToLargeChangeTrack, sView.PageIndex * PageSize * NumPagesInLargeChangeRows, pScrollview.Table
        pWorksheetNumbers.SetupGrid sView.TrackIndex * sView.ColumnsToLargeChangeTrack, sView.PageIndex * PageSize * NumPagesInLargeChangeRows, pScrollview.Table
        pWorksheetNumbers.Painting = True
        pWorksheetHeaders.Painting = True
        pWorksheet.Painting = True
    Else
        ScreenLib.WindowMoveTo pWorksheet, 0 - sView.TrackPositionModX, 0 - sView.PagePositionModY
        ScreenLib.WindowMoveTo pWorksheetHeaders, 0 - sView.TrackPositionModX, 0
        ScreenLib.WindowMoveTo pWorksheetNumbers, 0, 0 - sView.PagePositionModY
    End If
    This = sView
End Sub


' --- STATE MANAGEMENT ---

Private Function GetViewportStateAt(ByVal X As Double, ByVal Y As Double) As TViewportState
    Dim t As TViewportState, maxTrackWidth As Long, cellWidth As Long, viewWidth As Long, cellHeight As Long
    
    t.ScrollPosX = X
    t.ScrollPosY = Y
    
    maxTrackWidth = Worksheet.MaxContentWidthLimit
    cellWidth = Worksheet.GridCellSizeX
    cellHeight = Worksheet.GridCellSizeY
    viewWidth = Scrollview.ScrollPageSizeX
    
    t.ColumnsToLargeChangeTrack = Max(CLng((maxTrackWidth - viewWidth) / cellWidth) - 1, 1)
    
    t.FirstVisibleColumn = CLng(Int(X / CDbl(cellWidth)))
    t.FirstVisibleRow = CLng(Int(Y / CDbl(cellHeight)))
    t.FirstVisibleColumnPositionModX = CLng(ModFunc(X, CDbl(cellWidth)))
    t.FirstVisibleRowPositionModY = CLng(ModFunc(Y, CDbl(cellHeight)))
    t.TrackIndex = CLng(Int(X / CDbl(cellWidth * t.ColumnsToLargeChangeTrack)))
    t.PageIndex = CLng(Int(t.FirstVisibleRow / (PageSize * NumPagesInLargeChangeRows)))
    t.FirstVisibleColumnInTrack = t.FirstVisibleColumn - (t.ColumnsToLargeChangeTrack * t.TrackIndex)
    t.FirstVisibleRowInPage = t.FirstVisibleRow - (t.PageIndex * PageSize * NumPagesInLargeChangeRows)
    t.TrackPositionModX = (t.FirstVisibleColumnInTrack * cellWidth) + t.FirstVisibleColumnPositionModX
    t.PagePositionModY = (t.FirstVisibleRowInPage * cellHeight) + t.FirstVisibleRowPositionModY
    
    GetViewportStateAt = t
End Function

Public Function GetScrollXTo(ByVal ColumnIndex As Long) As Double
    Dim cellWidth As Long, viewWidth As Long, curViewMinX As Double, curViewMaxX As Double
    Dim targetCellMinX As Double, targetCellMaxX As Double, X As Double
    
    cellWidth = Worksheet.GridCellSizeX
    viewWidth = Scrollview.ScrollPageSizeX
    
    X = CDbl(cellWidth) * CDbl(ColumnIndex)
    targetCellMinX = Max(X - CDbl(cellWidth), 0)
    targetCellMaxX = X + CDbl(3 * cellWidth)
    
    curViewMinX = This.ScrollPosX
    curViewMaxX = curViewMinX + CDbl(viewWidth)
    
    X = This.ScrollPosX
    If curViewMinX > targetCellMinX Then
        X = targetCellMinX
    ElseIf curViewMaxX < targetCellMaxX Then
        X = targetCellMaxX - CDbl(viewWidth)
    End If
    
    GetScrollXTo = X
End Function

Public Function GetScrollYTo(ByVal RowIndex As Long) As Double
    Dim cellHeight As Long, viewHeight As Long, curViewMinY As Double, curViewMaxY As Double
    Dim targetCellMinY As Double, targetCellMaxY As Double, Y As Double
    
    cellHeight = Worksheet.GridCellSizeY
    viewHeight = Scrollview.ScrollPageSizeY
    
    Y = CDbl(cellHeight) * CDbl(RowIndex)
    targetCellMinY = Max(Y - CDbl(cellHeight), 0)
    targetCellMaxY = Y + CDbl(3 * cellHeight)
    
    curViewMinY = This.ScrollPosY
    curViewMaxY = curViewMinY + CDbl(viewHeight)
    
    Y = This.ScrollPosY
    If curViewMinY > targetCellMinY Then
        Y = targetCellMinY
    ElseIf curViewMaxY < targetCellMaxY Then
        Y = targetCellMaxY - CDbl(viewHeight)
    End If
    
    GetScrollYTo = Y
End Function

Public Sub OnSourceTableChange()
    This.TrackIndex = -1
    If pScrollview.KeepScrollPositionOnTableChange Then
        ScrollTo This.ScrollPosX, This.ScrollPosY
    Else
        On Error GoTo Finally
        pScrollview.IgnoreScrollingEvents = True
        pScrollview.ScrollPosX = 0#
        pScrollview.ScrollPosY = 0#
        ScrollTo 0#, 0#
Finally:
        pScrollview.IgnoreScrollingEvents = False
    End If
End Sub

Private Function GetTrack(ByVal TrackIndex As Long, ByVal PageIndex As Long) As RecordsetEx
    Dim dsT As dsTable, rX As RecordsetEx, ColumnStartIndex As Long, nCols As Long, dsT2 As dsTable, dsT3 As dsTable, nRows As Long, RowStartIndex As Long

    Set dsT = Scrollview.Table
    ColumnStartIndex = TrackIndex * pTrackColumnSizesInCache
    nCols = Worksheet.MaxAvailColumns
    
    If ColumnStartIndex >= dsT.ColumnCount Then
        Set rX = RecordsetEx.CreateBlank(PageSize * PageCount, nCols)
    Else
        RowStartIndex = Min(PageSize * PageIndex * NumPagesInLargeChangeRows, dsT.Count)
        nRows = Min(dsT.Count - RowStartIndex, PageSize * PageCount)
        If dsT.ColumnCount - ColumnStartIndex > nCols Then
            If nRows < PageSize * PageCount Then
                Set dsT2 = dsT.GetRange(RowStartIndex, nRows, CollectionsLib.ArrayRange(ColumnStartIndex, nCols))
                Set dsT2 = dsT2.AddRange(dsTable.CreateBlank((PageSize * PageCount) - nRows, nCols))
                Set rX = RecordsetEx.Create(dsT2.IndexRecordset)
            Else
                Set rX = RecordsetEx.Create(dsT.CreateIndexRecordset(PageSize, PageIndex * NumPagesInLargeChangeRows, PageCount, ColumnStartIndex, nCols, True))
            End If
        Else
            Set dsT2 = dsT.GetRange(RowStartIndex, nRows, CollectionsLib.ArrayRange(ColumnStartIndex, dsT.ColumnCount))
            Set dsT3 = dsTable.CreateBlank(nRows, nCols - (dsT.ColumnCount - ColumnStartIndex))
            Set dsT2 = dsT2.Join(dsT3)
            If nRows < PageSize * PageCount Then
                Set dsT2 = dsT2.AddRange(dsTable.CreateBlank((PageSize * PageCount) - nRows, nCols))
            End If
            Set rX = RecordsetEx.Create(dsT2.IndexRecordset)
        End If
    End If
    
    Set GetTrack = rX
End Function


' --- HELPERS ---

Private Function Max(X As Variant, Y As Variant) As Variant: Max = IIf(X > Y, X, Y): End Function
Private Function Min(X As Variant, Y As Variant) As Variant: Min = IIf(X < Y, X, Y): End Function
Private Function ModFunc(X As Variant, Y As Variant) As Variant: ModFunc = X - (Fix(X / Y) * Y): End Function


' --- DEBUG ---

Public Sub Debugger()
    Stop
End Sub
